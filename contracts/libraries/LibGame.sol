// SPDX-License-Identifier: MIT
pragma solidity ^0.8.16;
/**
 ⠀⠀⢀⣠⠤⠶⠖⠒⠒⠶⠦⠤⣄⠀⠀⠀⣀⡤⠤⠤⠤⠤⣄⡀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⣴⠋⠀⠀⠀⠀⠀⠀⠀⠀⠀⠈⠙⣦⠞⠁⠀⠀⠀⠀⠀⠀⠉⠳⡄⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⡾⠁⠀⠀⠀⠀⠀⠀⣀⣀⣀⣀⣀⣀⣘⡆⠀⠀⠀⠀⠀⠀⠀⠀⠀⠙⣆⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⢀⡴⠚⠉⠁⠀⠀⠀⠀⠈⠉⠙⠲⣄⣤⠤⠶⠒⠒⠲⠦⢤⣜⣧⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠉⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠈⠳⡄⠀⠀⠀⠀⠀⠀⠀⠉⠳⢄⡀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⣀⣀⣀⣀⣀⣀⣀⣀⣀⣀⣀⣀⠹⣆⠀⠀⠀⠀⠀⠀⣀⣀⣀⣹⣄⠀⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⣠⠞⣉⣡⠤⠴⠿⠗⠳⠶⣬⣙⠓⢦⡈⠙⢿⡀⠀⠀⢀⣼⣿⣿⣿⣿⣿⡿⣷⣤⡀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⣾⣡⠞⣁⣀⣀⣀⣠⣤⣤⣤⣄⣭⣷⣦⣽⣦⡀⢻⡄⠰⢟⣥⣾⣿⣏⣉⡙⠓⢦⣻⠃⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠉⠉⠙⠻⢤⣄⣼⣿⣽⣿⠟⠻⣿⠄⠀⠀⢻⡝⢿⡇⣠⣿⣿⣻⣿⠿⣿⡉⠓⠮⣿⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠙⢦⡈⠛⠿⣾⣿⣶⣾⡿⠀⠀⠀⢀⣳⣘⢻⣇⣿⣿⣽⣿⣶⣾⠃⣀⡴⣿⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠙⠲⠤⢄⣈⣉⣙⣓⣒⣒⣚⣉⣥⠟⠀⢯⣉⡉⠉⠉⠛⢉⣉⣡⡾⠁⠀⠀⠀⠀⠀⠀⠀
⠀⠀⣠⣤⡤⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⢈⡿⠋⠀⠀⠀⠀⠈⠻⣍⠉⠀⠺⠿⠋⠙⣦⠀⠀⠀⠀⠀⠀⠀
⠀⣀⣥⣤⠴⠆⠀⠀⠀⠀⠀⠀⠀⣀⣠⠤⠖⠋⠀⠀⠀⠀⠀⠀⠀⠀⠈⠳⠀⠀⠀⠀⠀⢸⣧⠀⠀⠀⠀⠀⠀
⠸⢫⡟⠙⣛⠲⠤⣄⣀⣀⠀⠈⠋⠉⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⣠⠏⣨⠇⠀⠀⠀⠀⠀
⠀⠀⠻⢦⣈⠓⠶⠤⣄⣉⠉⠉⠛⠒⠲⠦⠤⠤⣤⣀⣀⣀⣀⣀⣀⣀⣀⣀⣀⣀⣠⠴⢋⡴⠋⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠉⠓⠦⣄⡀⠈⠙⠓⠒⠶⠶⠶⠶⠤⣤⣀⣀⣀⣀⣀⣉⣉⣉⣉⣉⣀⣠⠴⠋⣿⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠉⠓⠦⣄⣀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⢀⡼⠁⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠉⠉⠙⠛⠒⠒⠒⠒⠒⠤⠤⠤⠒⠒⠒⠒⠒⠒⠚⢉⡇⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⣠⠴⠚⠛⠳⣤⠞⠁⠀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⢀⣤⠚⠁⠀⠀⠀⠀⠘⠲⣄⡀⠀⠀⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⣴⠋⠙⢷⡋⢙⡇⢀⡴⢒⡿⢶⣄⡴⠀⠙⠳⣄⠀⠀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠙⢦⡀⠈⠛⢻⠛⢉⡴⣋⡴⠟⠁⠀⠀⠀⠀⠈⢧⡀⠀⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⢻⡄⠀⠘⣶⢋⡞⠁⠀⠀⢀⡴⠂⠀⠀⠀⠀⠹⣄⠀⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⡇⠀⠀⠈⠻⢦⡀⠀⣰⠏⠀⠀⢀⡴⠃⢀⡄⠙⣆⠀
⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⠀⢠⡾⢷⡄⠀⠀⠀⠀⠉⠙⠯⠀⠀⡴⠋⠀⢠⠟⠀⠀⢹⡄
 */
import {LibDistance} from './LibDistance.sol';
import {LibMath} from './LibMath.sol';
import {LibState, State, Game, Mob, Tower} from '../libraries/LibState.sol';
import {MAP_WIDTH, MOB_BASE_LIFE, MOB_LIFE_MODIFIER, MOB_BASE_SPEED, MOB_SPEED_MODIFIER, MOB_BASE_DAMAGE} from '../Constants.sol';

library LibGame {
    function move_mobs(
        Game storage g,
        uint256 _amount,
        uint8[] memory MOB_PATH // to heavy to instantiate on each tick
    ) internal {
        for (uint256 i = 0; i < _amount; i++) {
            Mob storage mob = g.mobs[i];

            if (mob.life == 0 || mob.reached_goal) continue;

            mob.steps += mob.speed;

            // if mob is switching cell
            if (mob.steps >= 100) {
                // reset the steps but keep advance
                // if the mob walked 114 steps we set it at 14 steps for the new cell
                mob.steps -= 100;
                mob.cell_id = MOB_PATH[mob.target_cell_index];
                mob.target_cell_index++;

                if (mob.target_cell_index >= MOB_PATH.length) {
                    // mob reached the end of is path
                    mob.reached_goal = true;

                    if (g.life - mob.damage <= 0) {
                        // game over
                        g.life = 0;
                        return;
                    }

                    g.life -= mob.damage;
                }
            }
        }
    }

    function _damage_mob(Mob storage _mob, uint256 _damage) private {
        if (_mob.life <= _damage) _mob.life = 0;
        else _mob.life -= _damage;
    }

    function _mob_can_be_damaged(
        Mob storage _mob,
        uint8 _tower_cell_id,
        uint8 _range
    ) private view returns (bool) {
        if (_mob.reached_goal || _mob.life == 0) return true;
        return
            LibDistance.is_in_range(
                _mob.cell_id,
                _tower_cell_id,
                _range,
                MAP_WIDTH
            );
    }

    function _tower_can_fire(Tower memory _tower, uint256 _tick)
        private
        pure
        returns (bool)
    {
        return _tower.last_fired + _tower.fire_rate <= _tick;
    }

    function compute_towers(Game storage g, uint256 _mobs_spawned) internal {
        uint256 tick = g.tick;
        uint8[] memory cells_with_towers = g.cells_with_towers;
        for (
            uint8 tower_cell_index = 0;
            tower_cell_index < cells_with_towers.length;
            tower_cell_index++
        ) {
            uint8 tower_cell_id = cells_with_towers[tower_cell_index];

            for (
                uint256 mob_index = 0;
                mob_index < _mobs_spawned;
                mob_index++
            ) {
                Mob storage mob = g.mobs[mob_index];
                Tower memory tower = g.towers[tower_cell_id];
                if (
                    _mob_can_be_damaged(mob, tower_cell_id, tower.range) &&
                    _tower_can_fire(tower, tick)
                ) {
                    // updating storage
                    g.towers[tower_cell_id].last_fired = tick;
                    _damage_mob(mob, tower.damage);
                }
            }
        }
    }

    function generate_mob(uint256 _wave)
        internal
        pure
        returns (Mob memory mob)
    {
        mob.life = LibMath.curve(MOB_BASE_LIFE, MOB_LIFE_MODIFIER, _wave);
        mob.speed = LibMath.curve(MOB_BASE_SPEED, MOB_SPEED_MODIFIER, _wave);
        mob.damage = MOB_BASE_DAMAGE;
        mob.target_cell_index = 1;
    }

    function generate_tower(
        uint256 _damage,
        uint8 _range,
        uint8 _fire_rate
    ) internal pure returns (Tower memory tower) {
        tower.damage = _damage;
        tower.range = _range;
        tower.fire_rate = _fire_rate;
    }

    function wave_in_progress(
        Game storage g,
        uint256 _mobs_spawned,
        uint256 _mobs_amount
    ) internal view returns (bool) {
        if (g.life == 0) return false;
        if (_mobs_spawned < _mobs_amount) return true;

        // has mob on terrain
        for (uint256 i = 0; i < _mobs_spawned; i++) {
            Mob storage mob = g.mobs[i];
            // if the mob is alive and didn't reached the goal
            if (mob.life > 0 && !mob.reached_goal) return true;
        }
        return false;
    }

    function compute_score(Game storage g) internal view returns (uint256) {
        uint256 tower_amount = g.cells_with_towers.length;
        // define the cost of each tower later
        uint256 total_tower_cost = tower_amount * 100;
        return total_tower_cost * g.life * g.wave;
    }

    function is_on_mob_path(uint8 _cell_id) private view returns (bool) {
        uint8[] memory MOB_PATH = LibState.slot().MOB_PATH;
        for (uint8 i = 0; i < MOB_PATH.length; i++) {
            if (MOB_PATH[i] == _cell_id) return true;
        }
        return false;
    }

    function place_tower(
        Game storage g,
        uint8 _cell_id,
        uint256 _damage,
        uint8 _range,
        uint8 _fire_rate
    ) internal {
        require(!is_on_mob_path(_cell_id), 'Placing tower on mobs path');

        delete g.towers[_cell_id];
        g.towers[_cell_id] = generate_tower(_damage, _range, _fire_rate);
        g.cells_with_towers.push(_cell_id);
    }
}
